#!/bin/sh
#|-*- mode:lisp -*-|#
#|
exec ros -Q -- $0 "$@"
|#
(progn ;;init forms
  (ros:ensure-asdf)
  #+quicklisp(ql:quickload '(:alexandria :cl-ppcre) :silent t)
  )

(defpackage :ros.script.gen-make.3858474593
  (:use :cl))
(in-package :ros.script.gen-make.3858474593)

(defstruct (system-spec (:conc-name ".")) project system-file system-name dependencies)

(defun parse-line (line)
  (uiop:split-string line :separator " "))

(defun parse-file (file function)
  (with-open-file (in file)
    (loop :for line := (read-line in nil)
          :while line
          :unless (alexandria:starts-with-subseq "#" line)
          :collect (funcall function (parse-line line)))))

(defun load-systems.txt (systems.txt)
  (parse-file systems.txt
              (lambda (parts)
                (destructuring-bind (project system-file system-name &rest dependencies)
                    parts
                  (make-system-spec :project project
                                    :system-file system-file
                                    :system-name system-name
                                    :dependencies dependencies)))))

(defun escape-system-name (name)
  (ppcre:regex-replace-all "/" name "_"))

(defun system-to-target-name (system-name directory)
  (make-pathname :name (escape-system-name system-name) :type "db" :defaults directory))

(defun gen-makefile (systems.txt dist-dir index-directory stream)
  (let ((system-specs (load-systems.txt systems.txt)))
    (format stream "all: ~{~A~^\\~%~5T~}~%"
            (mapcar (alexandria:compose (alexandria:rcurry #'system-to-target-name index-directory)
                                        #'.system-name)
                    system-specs))
    (dolist (system-spec system-specs)
      (let ((target-name (system-to-target-name (.system-name system-spec) index-directory)))
        (format stream "~A:" target-name)
        (when (.dependencies system-spec)
          (format stream
                  " ~{~A~^ ~}"
                  (mapcar (alexandria:rcurry #'system-to-target-name index-directory)
                          (.dependencies system-spec))))
        (format stream
                "~%~C./searty-index.sh ~A ~A ~A~%"
                #\tab
                (.system-name system-spec)
                dist-dir
                target-name)))))

(defun main (&rest argv)
  (declare (ignorable argv))
  (destructuring-bind (systems.txt dist-dir index-directory makefile) argv
    (with-open-file (out makefile :direction :output :if-exists :supersede)
      (gen-makefile systems.txt
                    (uiop:ensure-directory-pathname dist-dir)
                    (uiop:ensure-directory-pathname index-directory)
                    out))))

;;; vim: set ft=lisp lisp:
