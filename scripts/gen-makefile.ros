#!/bin/sh
#|-*- mode:lisp -*-|#
#|
exec ros -Q -- $0 "$@"
|#
(progn ;;init forms
  (ros:ensure-asdf)
  #+quicklisp(ql:quickload '(:alexandria) :silent t)
  )

(defpackage :ros.script.gen-make.3858474593
  (:use :cl))
(in-package :ros.script.gen-make.3858474593)

(defstruct (system-spec (:conc-name ".")) project system-file system-name dependencies)

(defun parse-line (line)
  (uiop:split-string line :separator " "))

(defun parse-file (file function)
  (with-open-file (in file)
    (loop :for line := (read-line in nil)
          :while line
          :unless (alexandria:starts-with-subseq "#" line)
          :collect (funcall function (parse-line line)))))

(defun load-systems.txt (systems.txt)
  (parse-file systems.txt
              (lambda (parts)
                (destructuring-bind (project system-file system-name &rest dependencies)
                    parts
                  (make-system-spec :project project
                                    :system-file system-file
                                    :system-name system-name
                                    :dependencies dependencies)))))

(defun system-to-target-name (system-name output-dir)
  (merge-pathnames "searty.db"
                   (uiop:ensure-directory-pathname (merge-pathnames system-name output-dir))))

(defun gen-makefile (systems.txt dist-dir output-dir stream)
  (let ((system-specs (load-systems.txt systems.txt)))
    (format stream "all: ~{~A~^\\~%~5T~}~%"
            (mapcar (alexandria:compose (alexandria:rcurry #'system-to-target-name output-dir)
                                        #'.system-name)
                    system-specs))
    (dolist (system-spec system-specs)
      (format stream "~A:" (system-to-target-name (.system-name system-spec) output-dir))
      (when (.dependencies system-spec)
        (format stream
                " ~{~A~^ ~}"
                (mapcar (alexandria:rcurry #'system-to-target-name output-dir)
                        (.dependencies system-spec))))
      (format stream
              "~%~C./searty-index.sh ~A ~A ~A~%"
              #\tab
              (.system-name system-spec)
              dist-dir
              output-dir))))

(defun main (&rest argv)
  (declare (ignorable argv))
  (destructuring-bind (systems.txt dist-dir output-dir makefile) argv
    (with-open-file (out makefile :direction :output :if-exists :supersede)
      (gen-makefile systems.txt
                    (uiop:ensure-directory-pathname dist-dir)
                    (uiop:ensure-directory-pathname output-dir)
                    out))))

;;; vim: set ft=lisp lisp:
